apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        applicationId "com.linwei.camsmodular"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexKeepProguard file("multidexKeep.pro")
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                //Hilt plugin 版本兼容问题，这里需要使用arguments+=
                arguments += [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        ndk {
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        androidTest {
            java.srcDirs = ['tests']
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding false
    }

    dynamicFeatures = []
}

//设置编译编码
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
    correctErrorTypes true
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    /*ARouter*/
    kapt rootProject.ext.dependencies["arouter-compiler"]
    /*Hilt*/
    implementation rootProject.ext.dependencies["hilt-android"]
    kapt rootProject.ext.dependencies["hilt-compiler"]
    kapt rootProject.ext.dependencies["hilt-androidx-compiler"]

    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-junit"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]

}